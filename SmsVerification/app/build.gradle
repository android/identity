plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.google.services)
}

android {
    namespace "com.google.samples.smartlock.sms_verify"
    compileSdk 35
    defaultConfig {
        applicationId "com.google.samples.smartlock.sms_verify"
        minSdk 23
        targetSdk 35
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    signingConfigs {
        debug {
            // Note: mutual dependency between this file, and SMS generated by server.
            // See README.md.
            if (new File('../debug.keystore').exists()) {
                storeFile file('../debug.keystore')
            }
        }
    }
}

dependencies {
    constraints {
        //noinspection ForeignDelegate
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        //noinspection ForeignDelegate
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }

    implementation libs.androidx.appcompat
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.localbroadcastmanager
    implementation libs.google.play.services.base
    implementation libs.google.play.services.identity
    implementation libs.google.play.services.auth
    implementation libs.google.play.services.auth.api.phone
    implementation libs.volley

    testImplementation libs.junit
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.androidx.test.core
    androidTestImplementation libs.androidx.test.monitor
    androidTestImplementation libs.androidx.test.espresso.core
}
